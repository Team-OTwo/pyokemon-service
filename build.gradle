plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.0' apply false
    id 'io.spring.dependency-management' version '1.1.4' apply false
    id("com.diffplug.spotless") version "6.25.0"
}


allprojects {
    group = 'com.pyokemon'
    version = '0.0.1-SNAPSHOT'
    
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.diffplug.spotless'
    
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }
    
    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
        all {
            exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        }
    }
    
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        implementation 'org.springframework.boot:spring-boot-starter-log4j2'
        
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
    }

    spotless {
        java {
            eclipse().configFile("${rootProject.projectDir}/eclipse-java-google-style.xml")
            importOrder 'java', 'jakarta', 'javax', 'org', 'com', ''
        }
    }

    test {
        useJUnitPlatform()
    }
    
    build.dependsOn spotlessApply
}

// Task to install git hooks
task installGitHooks(type: Copy) {
    from file("${rootProject.projectDir}/githook/pre-commit")
    into file("${rootProject.projectDir}/.git/hooks")
    fileMode 0755 // Make the hook executable
}

// Root project build also depends on spotlessApply and installGitHooks
build.dependsOn spotlessApply
build.dependsOn installGitHooks